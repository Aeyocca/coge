package CoGe::Builder::SNP::Platypus;

use v5.14;
use warnings;
use strict;

use Carp;
use Data::Dumper;
use File::Spec::Functions qw(catdir catfile);

use CoGe::Accessory::Web;
use CoGe::Accessory::Jex;
use CoGe::Accessory::Utils qw(to_filename);
use CoGe::Core::Storage qw(get_genome_file get_experiment_files get_workflow_paths);
use CoGe::Builder::CommonTasks;

require Exporter;
our @ISA = qw(Exporter);
our @EXPORT = qw(build run);
our $CONF = CoGe::Accessory::Web::get_defaults();
our $JEX = CoGe::Accessory::Jex->new( host => $CONF->{JOBSERVER}, port => $CONF->{JOBPORT} );

sub run {
    my %opts = @_;
    my $user = $opts{user};
    my $genome = $opts{genome};
    my $input_file = $opts{input_file};
    my $metadata = $opts{metadata};
    croak "Missing parameters" unless ($user and $genome and $input_file and $metadata);

    # Create the workflow
    my $workflow = $JEX->create_workflow( name => 'Running the Playtpus SNP-finder pipeline', init => 1 );
    my ($staging_dir, $result_dir) = get_workflow_paths( $user->name, $workflow->id );
    $workflow->logfile( catfile($result_dir, 'debug.log') );

    # Build the workflow
    my @jobs = build({
        staging_dir => $staging_dir,
        result_dir => $result_dir,
        user => $user,
        wid  => $workflow->id,
        genome => $genome,
        input_file => $input_file,
        metadata => $metadata,
    });
    $workflow->add_jobs(\@jobs);

    # Submit the workflow
    my $result = $JEX->submit_workflow($workflow);
    if ($result->{status} =~ /error/i) {
        return (undef, "Could not submit workflow");
    }

    return ($result->{id}, undef);
}

sub build {
    my $opts = shift;

    # Required arguments
    my $genome = $opts->{genome};
    my $input_file = $opts->{input_file}; # path to bam file
    my $user = $opts->{user};
    my $wid = $opts->{wid};
    my $staging_dir = $opts->{staging_dir};
    my $result_dir = $opts->{result_dir};
    my $metadata = $opts->{metadata};

    my $gid = $genome->id;
    my $FASTA_CACHE_DIR = catdir($CONF->{CACHEDIR}, $gid, "fasta");
    die "ERROR: CACHEDIR not specified in config" unless $FASTA_CACHE_DIR;
    my $fasta_file = get_genome_file($gid);
    my $reheader_fasta =  to_filename($fasta_file) . ".reheader.faa";

    my $conf = {
        staging_dir => $staging_dir,
        result_dir  => $result_dir,

        bam         => $input_file,
        fasta       => catfile($FASTA_CACHE_DIR, $reheader_fasta),
        vcf         => catfile($staging_dir, qq[snps.vcf]),

        annotations => generate_experiment_metadata(),
        username    => $user->name,
        metadata    => $metadata,
        wid         => $wid,
        gid         => $gid
    };

    # Build all the jobs
    my @jobs;
    push @jobs, create_fasta_reheader_job(
        fasta => $fasta_file,
        reheader_fasta => $reheader_fasta,
        cache_dir => $FASTA_CACHE_DIR
    );

    push @jobs, create_fasta_index_job(
        fasta => catfile($FASTA_CACHE_DIR, $reheader_fasta),
        cache_dir => $FASTA_CACHE_DIR,
    );
    
    push @jobs, create_bam_index_job(
        input_bam => $input_file
    );

    push @jobs, create_platypus_job($conf);
    push @jobs, create_load_vcf_job($conf);

    return wantarray ? @jobs : \@jobs;
}

sub create_platypus_job {
    my $opts = shift;

    # Required arguments
    my $reference = $opts->{fasta};
    my $alignment = $opts->{bam};
    my $vcf = $opts->{vcf};

    my $index = qq[$reference.fai];
    my $PLATYPUS = $CONF->{PLATYPUS} || "Platypus.py";

    return {
        cmd => qq[$PLATYPUS callVariants],
        args =>  [
            ["--bamFiles", $alignment, 0],
            ["--refFile", $reference, 0],
            ["--output", $vcf, 1],
            ["--verbosity", 0, 0],
        ],
        inputs => [
            $alignment,
            $reference,
            $index,
        ],
        outputs => [
            $vcf,
        ],
        description => "Identifying SNPs using Platypus method ..."
    };
}

sub generate_experiment_metadata {
    my @annotations = (
        qq{http://genomevolution.org/wiki/index.php/Identifying_SNPs||note|Generated by CoGe's SNP-finder Pipeline (Platypus method)},
    );
    return join(';', @annotations);
}

1;
