package CoGe::Builder::SNP::CoGeSNPs;

use Moose;
extends 'CoGe::Builder::SNP::Analyzer';

use Data::Dumper qw(Dumper);
use File::Spec::Functions qw(catfile catdir);

use CoGe::Accessory::Web;
use CoGe::Accessory::Utils;
use CoGe::Core::Storage;
use CoGe::Core::Metadata;
use CoGe::Exception::Generic;

sub build {
    my $self = shift;
    my %opts = @_;
    my $fasta_file = $opts{fasta_file};
    my ($bam_file) = @{$opts{data_files}};

    unless ($fasta_file) {
        CoGe::Exception::Generic->throw(message => 'Missing fasta');
    }
    unless ($bam_file) {
        CoGe::Exception::Generic->throw(message => 'Missing bam');
    }

    my $gid = $self->request->genome->id;

    #
    # Build workflow
    #

    $self->add(
        $self->samtools_mpileup(
            fasta_file => $fasta_file,
            bam_file => $bam_file
        )
    );

    $self->vcf($self->previous_output);
    
    $self->add(
        $self->load_vcf(
            gid => $gid,
            vcf => $self->vcf,
            annotations => $self->generate_additional_metadata() #TODO use metadata file instead
        )
    );
}

sub samtools_mpileup {
    my $self = shift;
    my %opts = @_;
    my $fasta_file = $opts{fasta_file};
    my $bam_file   = $opts{bam_file};

    my $params = $self->params->{snp_params};
    my $min_read_depth   = $params->{'min-read-depth'} || 10;
    my $min_base_quality = $params->{'min-base-quality'} || 20;
    my $min_allele_freq  = $params->{'min-allele-freq'} || 0.1;
    my $min_allele_count = $params->{'min-allele-count'} || 4;
    my $scale            = $params->{scale} || 32;
    
    my $samtools = get_command_path('SAMTOOLS');
    my $filter_script = catfile($self->conf->{SCRIPTDIR}, 'pileup_SNPs.pl');
    $filter_script .= ' min_read_depth=' . $min_read_depth;
    $filter_script .= ' min_base_quality=' . $min_base_quality;
    $filter_script .= ' min_allele_freq=' . $min_allele_freq;
    $filter_script .= ' min_allele_count=' . $min_allele_count;
    $filter_script .= ' quality_scale=' . $scale;
    
    my $output_file = to_filename_base($bam_file) . '.vcf';

    return {
        cmd => $samtools,
        args => [
            ['mpileup', '',              0],
            ['-f',      '',              0],
            ['',        $fasta_file,     1],
            ['',        $bam_file,       1],
            ['|',       $filter_script,  0],
            ['>',       $output_file,    1]
        ],
        inputs => [
            $fasta_file,
            $fasta_file.'.fai',
            $bam_file
        ],
        outputs => [
            catfile($self->staging_dir, $output_file)
        ],
        description => "Identifying SNPs using the CoGe method"
    };
}

sub generate_additional_metadata {
    my $self = shift;
    my $params = $self->params->{snp_params};
    my $min_read_depth   = $params->{'min-read-depth'} || 10;
    my $min_base_quality = $params->{'min-base-quality'} || 20;
    my $min_allele_freq  = $params->{'min-allele-freq'} || 0.1;
    my $min_allele_count = $params->{'min-allele-count'} || 4;
    my $scale            = $params->{scale} || 32;

    my @annotations;
    push @annotations, qq{https://genomevolution.org/wiki/index.php?title=LoadExp%2B||note|Generated by CoGe's NGS Analysis Pipeline};
    push @annotations, (
        qq{note|SNPs generated using CoGe method},
        qq{note|Minimum read depth of $min_read_depth},
        qq{note|Minimum high-quality (PHRED >= $min_base_quality) allele count of $min_allele_count, FASTQ encoding $scale},
        qq{note|Minimum allele frequency of } . $min_allele_freq * 100 . '%'
    );
    return \@annotations;
}

1;
